apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'spoon'
apply plugin: 'com.getkeepsafe.dexcount'

def includeFirebase = hasProperty("isCI") || hasProperty("firebase")

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "org.walleth"

        minSdkVersion 15
        targetSdkVersion 25

        versionCode 17
        versionName "0.17"
        archivesBaseName = "WALLETH-$versionName"

        testInstrumentationRunner "org.walleth.infrastructure.AppReplacingRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "ETHERSCAN_APIKEY", '"' + file('../.ci/etherscan.apikey').text + '"'

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warning 'InvalidPackage' // for OKIO
        warning 'MissingTranslation'
    }

    flavorDimensions "fire", "store"

    productFlavors {
        forFDroid {
            dimension "store"
            buildConfigField 'String', 'STORE', '"fdroid"'
        }

        forAmazon {
            dimension "store"
            buildConfigField 'String', 'STORE', '"amazon"'
        }

        forPlay {
            dimension "store"
            buildConfigField 'String', 'STORE', '"play"'
        }

        if (includeFirebase) {
            withFirebase {
                dimension "fire"
            }
        }

        noFirebase {
            dimension "fire"
        }

    }


    android.variantFilter { variant ->
        def firebase = variant.getFlavors().get(0).name
        def store = variant.getFlavors().get(1).name

        if ((project.hasProperty("singleFlavor") && (store != 'forPlay'))
                || ((store == 'forAmazon' || store == 'forPlay') && firebase == 'noFirebase')
                || (store == 'forFDroid' && firebase != 'noFirebase')) {
            variant.setIgnore(true)
        }
    }

}

spoon {
    debug = true
    grantAllPermissions = true
}

configurations {

    // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
    androidTestCompile.exclude group: 'com.android.support', module: 'appcompat-v7'
    androidTestCompile.exclude group: 'com.android.support', module: 'design'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-v4'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-annotations'
    androidTestCompile.exclude group: 'com.android.support', module: 'preference-v7'
    androidTestCompile.exclude module: 'recyclerview-v7'
    androidTestCompile.exclude module: 'kotlin-stdlib'
}

dependencies {
    compile 'com.android.support:multidex:1.0.2'
    androidTestCompile  'com.android.support:multidex:1.0.2'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "org.ethereum:geth:1.6.7"

    compile "com.github.ligi.kethereum:functions:$kethereum_version"
    compile "com.github.ligi.kethereum:bip44:$kethereum_version"
    compile "com.github.ligi.kethereum:erc55:$kethereum_version"

    compile 'com.github.ligi:tracedroid:1.4'
    compile 'com.github.ligi:KAXT:0.17'
    compile 'com.github.ligi:KAXTUI:0.4'
    compile 'com.github.trezor.trezor-android:trezor-lib:v1.0.6'


    compile 'com.github.kenglxn.QRGen:android:2.3.0'
    compile 'com.google.zxing:core:3.3.0'

    compile 'com.jakewharton:process-phoenix:2.0.0'

    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'

    compile 'com.github.salomonbrys.kodein:kodein:4.1.0'
    compile 'com.github.salomonbrys.kodein:kodein-android:4.1.0'

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:preference-v14:$support_version"

    compile "com.chibatching.kotpref:kotpref:2.1.2"

    compile 'com.squareup.moshi:moshi:1.5.0'
    compile 'com.github.ligi:ExtraCompats:0.4'

    androidTestCompile 'com.google.truth:truth:0.34'

    androidTestCompile "org.mockito:mockito-core:2.8.47"
    androidTestCompile 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'

    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.github.ligi:trulesk:0.19'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.34'
    testCompile 'org.threeten:threetenbp:1.3.6'

    if (includeFirebase) {
        withFirebaseCompile "com.google.firebase:firebase-core:$firebase_version"
        withFirebaseCompile "com.google.firebase:firebase-crash:$firebase_version"
        withFirebaseCompile "com.google.firebase:firebase-messaging:$firebase_version"
    }
}

if (hasProperty("isCI")) {

    apply plugin: 'witness'

    dependencyVerification {
        verify = [


                'com.github.ligi:tracedroid:7166d047a3622676357645414751d911b0e507e78d0a9102e38b7500a44fda8b',
                'com.github.ligi:KAXT:4cfe2b683dacff9575799edb63a3d46ff647761bf76cb17702150856471538bc',
                'com.jakewharton:process-phoenix:229c481a90067878d9b0bceee54600fe2ea48a771c6ff1fb47f0d865cf660844',
                'com.jakewharton.threetenabp:threetenabp:44b81823de861287f666c92a61c9e55259350f5bff588fec7819027a0291ddbf',
                'com.squareup.okhttp3:okhttp:c1d57f913f74f61d424d4250a92723ba9a61affc12a0ab194d84cc179b472841',
                'com.github.salomonbrys.kodein:kodein:6727cb851a73a82fd5ded29877739749f7f89c26a23ababaf9a06d7cf9aa7bee',
                'com.github.salomonbrys.kodein:kodein-android:98b1998e2477eb715720ddf7f016a1b828ceb87c1f11ce72dad3d518629a0330',
                'com.android.support:appcompat-v7:ac1ebbc46589195dda3e0b1becfe410bafd75bdf3edd1cd9acf04850f3895830',
                'com.android.support:design:a3e83064fe99d0a4369f9b46d8bfbe77d0c3022fffdee4be3ac3857b87cc89e3',
                'com.android.support:cardview-v7:defc17032ffa600a82e1c7d96bb574aa5ed64e2b57e28414a245da7d6db0c666',
                'com.android.support:recyclerview-v7:375974a8724e359d97d77fa8522c614f813a3ac4583c1807f154a3f9a054b0a1',
                'com.android.support:preference-v14:970904d676ea886d0be459e7e5e36c0db650daefea5bbe80cc59146091905be5',
                'com.chibatching.kotpref:kotpref:af6df531f6344f3cba0ea91bcf13c15fca4dc7ee819ace03ed1a244d39ffcb51',
                'com.github.ligi:ExtraCompats:3e4315f0f3b35e6e6dd5f37494480fd7ceaa09546c4361c7e7ff5b8563c936e0',
                'org.jetbrains:annotations:ace2a10dc8e2d5fd34925ecac03e4988b2c0f851650c94b8cef49ba1bd111478',
                'com.android.support:support-v4:07d389154bcf73b47e514964df1578136b26cba78257b8a577a3ccb54beff0ae',
                'com.github.kenglxn.QRGen:android:7670317f4ec37cf2bb24b7d3932da83daafa2956e77cd2e92dccce86a66816d9',
                'com.github.kenglxn.QRGen:core:1db3f51189a71bb37ee0e97081a353b6c6b203ee73bb93c4372aa76e94f9d701',
                'com.squareup.okio:okio:734269c3ebc5090e3b23566db558f421f0b4027277c79ad5d176b8ec168bb850',
                'com.github.salomonbrys.kodein:kodein-core:4876960b936d6becb00e7b5c614ba9f4be2b7d5d22a8a2433583d87373c342f8',
                'com.android.support:transition:36c688825a8c0e6e879e18886de83dc90673322822d5b606ee302f70fb558e16',
                'com.android.support:support-annotations:aedf76962584adfaed2bd3fcaa22406461c4310237fc27e301128edaa2dba2fa',
                'com.android.support:support-compat:e02d781268dc60aab6638d8dc20156ea11ca20b962d294b85e6f1e8418cabfa7',
                'com.android.support:support-core-ui:6182278a6653e6c111c888963626cbb16f2d0022571cb239760475119e0b92a8',
                'com.android.support:preference-v7:b37d8193c65cf8eb3c4ba9cbfc6d5c96042177901622717fb089290bef784a58',
                'com.google.zxing:core:bba7724e02a997cec38213af77133ee8e24b0d5cf5fa7ecbc16a4fa93f11ee0d',
                'com.android.support:support-media-compat:cbed07d07e0e84fdb4b75712f5fd946229a8af155933c9a92e41db64d00791e0',
                'com.android.support:support-core-utils:32fac02eb2c20a77fa3e3bc3ede62392a19613f72b8f8e10f5dfa84bb4c89ea1',
                'com.android.support:support-fragment:541d6393c1e024453aca2a14f31bea0c7270ff4e2a02783f3528aa426367444d',
                'com.android.support:support-vector-drawable:13728f337f36d1c02d52198a6c20724edb447a0875454d829f95cb9eb4aa293b',
                'com.android.support:animated-vector-drawable:4bc46edf1946b32d518b41416d1734e915e0cbb28021de3b340527419b070691',
        ]
    }
}
if (includeFirebase) {
    apply plugin: 'com.google.gms.google-services'
}

apply plugin: 'com.github.ben-manes.versions'
